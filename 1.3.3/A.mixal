* rX  = CURRENT
* rI1 = Answer output location
* rI3 = Cursor

CARDSIZE	EQU	16			# Words on each card
MAXWDS		EQU	75*CARDSIZE		# Maximum length of input
PERM		ORIG	*+MAXWDS		# The input permutation
ANS		ORIG	*+MAXWDS		# Place for answer
OUTBUF		ORIG	*+24			# Place for printing
CARDS		EQU	16			# Unit number for card reader
PRINTER		EQU	18			# Unit number for printer
BEGIN		IN	PERM(CARDS)		# Read first card
		ENT2	0
		LDA	EQUALS
1H		JBUS	*(CARDS)		# Wait for cycle to complete
		CMPA	PERM+CARDSIZE-1,2
		JE	*+2			# Is it the last card?
		IN	PERM+CARDSIZE,2(CARDS)	# No, read another
		ENT1	OUTBUF
		JBUS	*(PRINTER)		# Print a copy of
		MOVE	PERM,2(CARDSIZE)	#   the input card
		OUT	OUTBUF(PRINTER)
		JE	1F
		INC2	CARDSIZE
		CMP2	=MAXWDS-CARDSIZE=
		JLE	1B			# Repeat until input is complete
		HLT	666			# Too much input!
1H		INC2	CARDSIZE-1		# At this point, rI2 words of
		ST2	SIZE			#   input are in PERM, PERM+1, ...
		ENT3	0			# A1. First pass
2H		LDAN	PERM,3			# Get next element of input
		CMPA	LPAREN(1:5)		# Is is "("?
		JNE	1F
		STA	PERM,3			# If so, tag it
		INC3	1			# Put the next nonblank input symbol
		LDXN	PERM,3			#   into rX
		JXZ	*-2
1H		CMPA	RPAREN(1:5)
		JNE	*+2
		STX	PERM,3			# Replace ")" by tagged rX
		INC3	1
		CMP3	SIZE			# Have all elements been processed?
		JL	2B
		LDA	LPAREN			# Prepare for main program
		ENT1	ANS			# rI1 = place to store next answer
OPEN		ENT3	0			# A2. Open
1H		LDXN	PERM,3			# Look for untagged element
		JXN	GO
		INC3	1
		CMP3	SIZE
		JL	1B
* All tagged. Now comes the output
DONE		CMP1	=ANS=
		JNE	*+2			# Is answer the identity permutation?
		MOVE	LPAREN(2)		# If so, change to "()"
		MOVE	=0=			# Put 23 words of blanks after answer
		MOVE	-1,1(22)
		ENT3	0
		OUT	ANS,3(PRINTER)
		INC3	24
		LDX	ANS,3			# Print as many lines as necessary
		JXNZ	*-3
		HLT
* Constants
LPAREN		ALF	"    ("
RPAREN		ALF	")    "
EQUALS		ALF	"    ="
*
GO		MOVE	LPAREN			# Open a cycle in the output
		MOVE	PERM,3
		STX	START
SUCC		STX	PERM,3			# Tag an element
		INC3	1			# Move one step to the right
		LDXN	PERM,3(1:5)		# A3. Store CURRENT (rX)
		JXN	1F			# Skip past blanks
		JMP	*-3
5H		STX	0,1			# Output CURRENT
		INC1	1
		ENT3	0			# Scan formula again
4H		CMPX	PERM,3(1:5)		# A4. Scan formula
		JE	SUCC			# Element = CURRENT?
1H		INC3	1			# Move to right
		CMP3	SIZE			# End of formula?
		JL	4B
		CMPX	START(1:5)		# A5. CURRENT = START?
		JNE	5B
CLOSE		MOVE	RPAREN			# A6. Close
		CMPA	-3,1			# Note: rA = "("    [-3???]
		JNE	OPEN
		INC1	-3			# Suppress singleton cycles [?]
		JMP	OPEN
* Unused refs (this should be automatic)
START		CON	0
SIZE		CON	0
		END	BEGIN
